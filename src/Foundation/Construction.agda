{-# OPTIONS --prop --exact-split #-}
module Foundation.Construction where

open import Foundation.Axiom
open import Foundation.FormulaSyntax as F using (Formula)
open import Foundation.Corollary as Cor hiding (âˆ…âˆˆğ’«[x]; xâˆˆğ’«[x])

open import Proposition.Sum
open import Logic

open import Axiom.UniqueChoice

âˆ… : set
âˆ… = elem (!choice !âˆ…)
âˆ…-def = âˆ§left (prop (!choice !âˆ…))

separate :
  (Ï• : set â†’ Formula)
  (p : âˆƒ Î» x â†’ âˆ€ z â†’ F.holds (Ï• z) â†’ z âˆˆ x)
  â†’ ---------------------------------
  set
separate Ï• p = elem (!choice (!separate Ï• p))
separate-def = Î» Ï• p â†’ âˆ§left (prop (!choice (!separate Ï• p)))

subset : (X : set)(Ï• : set â†’ Formula) â†’ set
subset X Ï• =
  separate (Î» x â†’ x F.âˆˆ X F.âˆ§ Ï• x) (X , Î» {_ (xâˆˆX , _) â†’ xâˆˆX})

subset-def : âˆ€ X Ï• x â†’ x âˆˆ subset X Ï• â†” x âˆˆ X âˆ§ F.holds (Ï• x)
subset-def X Ï• =
  separate-def (Î» x â†’ x F.âˆˆ X F.âˆ§ Ï• x) (X , Î» {_ (xâˆˆX , _) â†’ xâˆˆX})

open import Proof

subsetâŠ† : âˆ€ X Ï• â†’ subset X Ï• âŠ† X
subsetâŠ† X Ï• x xâˆˆsubset = âˆ§left $ âŸ¶ (subset-def X Ï• x) xâˆˆsubset

replace : 
  (Ï• : (X x y : set) â†’ F.Formula) â†’
  âˆ€ X (p : â‹€ x âˆˆ X , âˆƒ! Î» y â†’ elem (Ï• X x y)) â†’ 
  set
replace Ï• X fun-Ï• = elem (!choice (!replace Ï• X fun-Ï•))
replace-def = Î» Ï• X fun-Ï• â†’ âˆ§left (prop (!choice (!replace Ï• X fun-Ï•)))

[_â¸´_] : (a b : set) â†’ set
[ a â¸´ b ] = elem (!choice ![ a â¸´ b ])
[_â¸´_]-def = Î» a b â†’ âˆ§left (prop (!choice ![ a â¸´ b ]))

open import Operation.Binary
open import Logic.Property
open import Logic.Proof

instance
  Commutative-pairing : Commutative [_â¸´_]

comm â¦ƒ Commutative-pairing â¦„ x y =
  set-ext [ x â¸´ y ] [ y â¸´ x ] Î» z â†’
    proof z âˆˆ [ x â¸´ y ]
      âŒª _â†”_ âŒª z == x âˆ¨ z == y  :by: [ x â¸´ y ]-def z
      âŒª _==_ âŒª z == y âˆ¨ z == x :by: comm (z == x) (z == y)
      âŒª _â†”_ âŒª z âˆˆ [ y â¸´ x ]     :by: sym $ [ y â¸´ x ]-def z
    qed

[_] : (x : set) â†’ set
[ x ] = elem (!choice ![ x ])
[_]-def = Î» x â†’ âˆ§left (prop (!choice ![ x ]))

open import PropUniverses hiding (X; Y; Z; W)
open import Relation.Binary.Property hiding (_âŠ†_)
open import Function.Proof

instance
  Postfixâˆˆ[-] : UniversalPostfix [_] _âˆˆ_
  Irreflexiveâˆˆ : Irreflexive _âˆˆ_

  ReflexiveâŠ† : Reflexive _âŠ†_
  TransitiveâŠ† : Transitive _âŠ†_
  AntisymmetricâŠ† : Antisymmetric _âŠ†_

  ComposableâˆˆâŠ† : Composable ğ’°â‚€ _âˆˆ_ _âŠ†_

module ComposableâŠ† where
  open TransMakeComposable _âŠ†_ public
module Composableâˆˆ where
  open MakeComposable _âˆˆ_ public

UniversalPostfix.postfix Postfixâˆˆ[-] x = âŸµ ([ x ]-def x) (refl x)

irrefl â¦ƒ Irreflexiveâˆˆ â¦„ x xâˆˆx
  with foundation [ x ] (x , postfix [_] x)
irrefl â¦ƒ Irreflexiveâˆˆ â¦„ x xâˆˆx
  | z , (zâˆˆ[x] , Â¬yâˆˆz) with âŸ¶ ([ x ]-def z) zâˆˆ[x]
irrefl â¦ƒ Irreflexiveâˆˆ â¦„ x xâˆˆx
  | x , (_ , Â¬yâˆˆ[x]) | Id.refl .x = Â¬yâˆˆ[x] (x , (postfix [_] x , xâˆˆx))

refl â¦ƒ ReflexiveâŠ† â¦„ _ _ p = p
trans â¦ƒ TransitiveâŠ† â¦„ xâŠ†y yâŠ†z w wâˆˆx = yâŠ†z w (xâŠ†y w wâˆˆx)
antisym â¦ƒ AntisymmetricâŠ† â¦„ {x}{y} xâŠ†y yâŠ†x =
  set-ext x y Î» z â†’ xâŠ†y z , yâŠ†x z

Composable.rel ComposableâˆˆâŠ† = _âˆˆ_
Composable.compose ComposableâˆˆâŠ† {x} xâˆˆy yâŠ†z = yâŠ†z x xâˆˆy

ğ’«[_] : (x : set) â†’ set
ğ’«[ x ] = elem (!choice !ğ’«[ x ])
ğ’«[_]-def = Î» x â†’ âˆ§left (prop (!choice !ğ’«[ x ]))

âˆ…âˆˆğ’«[x] : âˆ€ x â†’ âˆ… âˆˆ ğ’«[ x ]
âˆ…âˆˆğ’«[x] x = Cor.âˆ…âˆˆğ’«[x] âˆ…-def ğ’«[ x ]-def

instance
  Postfixâˆˆğ’«[-] : UniversalPostfix ğ’«[_] _âˆˆ_

UniversalPostfix.postfix Postfixâˆˆğ’«[-] x = Cor.xâˆˆğ’«[x] ğ’«[ x ]-def

open import Proposition.Identity hiding (refl)
open import Proposition.Proof

variable
  a b c d : set
  x y z w : set
  X Y Z W : set

[â¸´]-== : [ a â¸´ b ] == [ a â¸´ c ] â†” b == c
âŸ¶ ([â¸´]-== {a}{b}{c}) p with âŸ¶ ([ a â¸´ c ]-def b) bâˆˆ[a,c]
  where bâˆˆ[a,c] : b âˆˆ [ a â¸´ c ]
        bâˆˆ[a,c] =
          have b âˆˆ [ a â¸´ b ] :from: âŸµ ([ a â¸´ b ]-def b) (âˆ¨right (refl b))
            âŸ¶ b âˆˆ [ a â¸´ c ] :by: Id.coe $ ap (b âˆˆ_) p
âŸ¶ [â¸´]-== p | âˆ¨right q = q
âŸ¶ ([â¸´]-== {a}{a}{c}) p | âˆ¨left (Id.refl a) =
  have c âˆˆ [ a â¸´ c ]    :from: âŸµ ([ a â¸´ c ]-def c) (âˆ¨right (refl c))
    âŸ¶ c âˆˆ [ a â¸´ a ]    :by: Id.coe $ ap (c âˆˆ_) $ sym p
    âŸ¶ c == a âˆ¨ c == a :by: âŸ¶ ([ a â¸´ a ]-def c)
    âŸ¶ c == a           :by: Id.coe $ idemp (c == a)
    âŸ¶ a == c           :by: sym
âŸµ ([â¸´]-== {a}) (Id.refl b) = refl [ a â¸´ b ]

[â¸´]-â‰  : âˆ€ a b â†’ [ a â¸´ b ] â‰  a
[â¸´]-â‰  a b p = irrefl a $
  Id.coe (ap (a âˆˆ_) p) (âŸµ ([ a â¸´ b ]-def a) (âˆ¨left (refl a)))

âŸ¦_â¸´_âŸ§ : (a b : set) â†’ set
âŸ¦ a â¸´ b âŸ§ = [ a â¸´ [ a â¸´ b ] ]
âŸ¦_â¸´_âŸ§-def = Î» a b â†’ âˆ§left (prop (!choice ![ a â¸´ [ a â¸´ b ] ]))

âŸ¦â¸´âŸ§-== : 
  âŸ¦ a â¸´ b âŸ§ == âŸ¦ c â¸´ d âŸ§ â†” a == c âˆ§ b == d
âŸµ âŸ¦â¸´âŸ§-== (Id.refl a , Id.refl b) = refl âŸ¦ a â¸´ b âŸ§
âŸ¶ (âŸ¦â¸´âŸ§-== {a}{b}{c}{d}) p
  with âŸ¶ (p' a) (âˆ¨left (refl a))
     | âŸ¶ (p' [ a â¸´ b ]) (âˆ¨right (refl [ a â¸´ b ]))
  where p' : (x : set) â†’ (x == a âˆ¨ x == [ a â¸´ b ]) â†” (x == c âˆ¨ x == [ c â¸´ d ])
        p' x =
          proof x == a âˆ¨ x == [ a â¸´ b ]
            âŒª _â†”_ âŒª x âˆˆ âŸ¦ a â¸´ b âŸ§           :by: sym $ âŸ¦ a â¸´ b âŸ§-def x
            âŒª _==_ âŒª x âˆˆ âŸ¦ c â¸´ d âŸ§          :by: ap (x âˆˆ_) p
            âŒª _â†”_ âŒª x == c âˆ¨ x == [ c â¸´ d ] :by: âŸ¦ c â¸´ d âŸ§-def x
          qed
âŸ¶ (âŸ¦â¸´âŸ§-== {a}{b}{c}{d}) p | âˆ¨left (Id.refl a) | âˆ¨left q =
  âŠ¥-recursion (a == a âˆ§ b == d) ([â¸´]-â‰  a b q)
âŸ¶ âŸ¦â¸´âŸ§-== p | âˆ¨left (Id.refl a) | âˆ¨right q =
  refl a , âŸ¶ [â¸´]-== q
âŸ¶ (âŸ¦â¸´âŸ§-== {.([ c â¸´ d ])} {b} {c} {d}) p
  | âˆ¨right (Id.refl .([ c â¸´ d ]))
  | âˆ¨left [[câ¸´d]â¸´b]==c =
  âŠ¥-recursion ([ c â¸´ d ] == c âˆ§ b == d) contr
  where contr : âŠ¥
        contr with foundation âŸ¦ c â¸´ d âŸ§
          (c , âŸµ (âŸ¦ c â¸´ d âŸ§-def c) (âˆ¨left (refl c)))
        contr | z , (zâˆˆâŸ¦c,dâŸ§ , min-âˆˆ) with âŸ¶ (âŸ¦ c â¸´ d âŸ§-def z) zâˆˆâŸ¦c,dâŸ§
        contr | c , (_ , min-âˆˆ) | âˆ¨left (Id.refl _) =
          min-âˆˆ ([ c â¸´ d ] , (
            âŸµ (âŸ¦ c â¸´ d âŸ§-def [ c â¸´ d ]) (âˆ¨right (refl [ c â¸´ d ])) , (
            have [ c â¸´ d ] âˆˆ [ [ c â¸´ d ] â¸´ b ]
              :from: âŸµ ([ [ c â¸´ d ] â¸´ b ]-def [ c â¸´ d ]) (âˆ¨left (refl [ c â¸´ d ]))
            âŸ¶ [ c â¸´ d ] âˆˆ c :by: Id.coe (ap ([ c â¸´ d ] âˆˆ_) [[câ¸´d]â¸´b]==c))))
        contr | .([ c â¸´ d ]) , (_ , min-âˆˆ) | âˆ¨right (Id.refl _) =
          min-âˆˆ (c , (âŸµ (âŸ¦ c â¸´ d âŸ§-def c) (âˆ¨left (refl c)) ,
                      âŸµ ([ c â¸´ d ]-def c) (âˆ¨left (refl c))))
âŸ¶ (âŸ¦â¸´âŸ§-== {.([ c â¸´ d ])} {b} {c} {d}) p
  | âˆ¨right (Id.refl .([ c â¸´ d ]))
  | âˆ¨right [[c,d],b]==[c,d] =
  âŠ¥-recursion ([ c â¸´ d ] == c âˆ§ b == d) ([â¸´]-â‰  [ c â¸´ d ] b [[c,d],b]==[c,d])

infixl 150 _âˆ©_
_âˆ©_ : (x y : set) â†’ set
x âˆ© y = separate (Î» z â†’ z F.âˆˆ x F.âˆ§ z F.âˆˆ y)
                 (x , Î» {z (zâˆˆx , _) â†’ zâˆˆx})
_âˆ©_-def : (x y : set) â†’ âˆ€ z â†’ z âˆˆ x âˆ© y â†” z âˆˆ x âˆ§ z âˆˆ y
x âˆ© y -def = separate-def (Î» z â†’ z F.âˆˆ x F.âˆ§ z F.âˆˆ y)
                          (x , Î» {z (zâˆˆx , _) â†’ zâˆˆx})

instance
  Idempotent-âˆ© : Idempotent _âˆ©_
  Commutative-âˆ© : Commutative _âˆ©_
  Associative-âˆ© : Associative _âˆ©_

idemp â¦ƒ Idempotent-âˆ© â¦„ x = set-ext (x âˆ© x) x Î» z â†’
  proof z âˆˆ x âˆ© x
    âŒª _â†”_ âŒª  z âˆˆ x âˆ§ z âˆˆ x :by: (x âˆ© x -def) z
    âŒª _==_ âŒª z âˆˆ x          :by: idemp (z âˆˆ x)
  qed

comm â¦ƒ Commutative-âˆ© â¦„ x y = set-ext (x âˆ© y) (y âˆ© x) Î» z â†’
  proof z âˆˆ x âˆ© y
    âŒª _â†”_ âŒª  z âˆˆ x âˆ§ z âˆˆ y :by: (x âˆ© y -def) z
    âŒª _==_ âŒª z âˆˆ y âˆ§ z âˆˆ x :by: comm (z âˆˆ x) (z âˆˆ y)
    âŒª _â†”_ âŒª  z âˆˆ y âˆ© x      :by: sym $ (y âˆ© x -def) z
  qed

open import Axiom.PropositionExtensionality

assoc â¦ƒ Associative-âˆ© â¦„ x y z = set-ext (x âˆ© (y âˆ© z)) (x âˆ© y âˆ© z) Î» w â†’
  proof w âˆˆ x âˆ© (y âˆ© z)
    âŒª _â†”_ âŒª  w âˆˆ x âˆ§ w âˆˆ y âˆ© z
      :by: (x âˆ© (y âˆ© z) -def) w
    âŒª _==_ âŒª w âˆˆ x âˆ§ (w âˆˆ y âˆ§ w âˆˆ z)
      :by: ap (w âˆˆ x âˆ§_) $ prop-ext $ (y âˆ© z -def) w
    âŒª _==_ âŒª w âˆˆ x âˆ§ w âˆˆ y âˆ§ w âˆˆ z
      :by: assoc (w âˆˆ x) (w âˆˆ y) (w âˆˆ z)
    âŒª _==_ âŒª w âˆˆ x âˆ© y âˆ§ w âˆˆ z
      :by: ap (_âˆ§ w âˆˆ z) $ prop-ext $ sym $ (x âˆ© y -def) w
    âŒª _â†”_ âŒª  w âˆˆ x âˆ© y âˆ© z
      :by: sym $ ((x âˆ© y) âˆ© z -def) w
  qed

infixl 148 _-_
_-_ : (x y : set) â†’ set
x - y = subset x (Î» z â†’ F.Â¬ z F.âˆˆ y)
_-_-def : (x y : set) â†’ âˆ€ z â†’ z âˆˆ x - y â†” z âˆˆ x âˆ§ Â¬ z âˆˆ y
x - y -def = subset-def x (Î» z â†’ F.Â¬ z F.âˆˆ y)

open import Axiom.FunctionExtensionality

infixr 175 â‹‚
â‹‚ : âˆ€ X (p : x âˆˆ X) â†’ set
â‹‚ {x} X xâˆˆX =
  separate (Î» z â†’ F.â‹€ y âˆˆ X , z F.âˆˆ y)
           (x , Î» _ p â†’ p x xâˆˆX)
â‹‚-def : âˆ€ X {x} (p : x âˆˆ X) â†’ âˆ€ z â†’ z âˆˆ â‹‚ X p â†” âˆ€ x â†’ x âˆˆ X â†’ z âˆˆ x
â‹‚-def X {x} p =
  separate-def (Î» z â†’ F.â‹€ y âˆˆ X , z F.âˆˆ y)
               (x , Î» _ q â†’ q x p)

infixr 175 â‹ƒ_
â‹ƒ_ : (X : set) â†’ set
â‹ƒ X = elem (!choice (!â‹ƒ X))
â‹ƒ-def = Î» X â†’ âˆ§left (prop (!choice (!â‹ƒ X)))

infixl 149 _âˆª_
_âˆª_ : (x y : set) â†’ set
x âˆª y = â‹ƒ [ x â¸´ y ]
_âˆª_-def : âˆ€ x y â†’ âˆ€ z â†’ z âˆˆ x âˆª y â†” z âˆˆ x âˆ¨ z âˆˆ y
(x âˆª y -def) z =
  proof z âˆˆ x âˆª y
    âŒª _â†”_ âŒª (âˆƒ Î» w â†’ w âˆˆ [ x â¸´ y ] âˆ§ z âˆˆ w)
      :by: â‹ƒ-def [ x â¸´ y ] z
    âŒª _==_ âŒª (âˆƒ Î» w â†’ (w == x âˆ¨ w == y) âˆ§ z âˆˆ w)
      :by: ap (Î» â€” â†’ âˆƒ Î» w â†’ â€” w âˆ§ z âˆˆ w) $
           fun-ext (Î» w â†’ prop-ext $ [ x â¸´ y ]-def w)
    âŒª _â†”_ âŒª z âˆˆ x âˆ¨ z âˆˆ y
      :by: step
  qed
  where step : âˆƒ (Î» w â†’ (w == x âˆ¨ w == y) âˆ§ z âˆˆ w) â†” z âˆˆ x âˆ¨ z âˆˆ y
        âŸ¶ step (x , (âˆ¨left (Id.refl x) , zâˆˆx)) = âˆ¨left zâˆˆx 
        âŸ¶ step (y , (âˆ¨right (Id.refl y) , zâˆˆy)) = âˆ¨right zâˆˆy
        âŸµ step (âˆ¨left p) = x , (âˆ¨left (refl x) , p)
        âŸµ step (âˆ¨right q) = y , (âˆ¨right (refl y) , q)

instance
  Idempotent-âˆª : Idempotent _âˆª_
  Commutative-âˆª : Commutative _âˆª_
  Associative-âˆª : Associative _âˆª_

Idempotent.idemp Idempotent-âˆª x = set-ext (x âˆª x) x Î» z â†’
  proof z âˆˆ x âˆª x
    âŒª _â†”_ âŒª z âˆˆ x âˆ¨ z âˆˆ x :by: (x âˆª x -def) z
    âŒª _==_ âŒª z âˆˆ x         :by: idemp (z âˆˆ x)
  qed

comm â¦ƒ Commutative-âˆª â¦„ x y = set-ext (x âˆª y) (y âˆª x) Î» z â†’
  proof z âˆˆ x âˆª y
    âŒª _â†”_ âŒª  z âˆˆ x âˆ¨ z âˆˆ y :by: (x âˆª y -def) z
    âŒª _==_ âŒª z âˆˆ y âˆ¨ z âˆˆ x :by: comm (z âˆˆ x) (z âˆˆ y)
    âŒª _â†”_ âŒª  z âˆˆ y âˆª x      :by: sym $ (y âˆª x -def) z
  qed

assoc â¦ƒ Associative-âˆª â¦„ x y z = set-ext (x âˆª (y âˆª z)) (x âˆª y âˆª z) Î» w â†’
  proof w âˆˆ x âˆª (y âˆª z)
    âŒª _â†”_ âŒª  w âˆˆ x âˆ¨ w âˆˆ y âˆª z
      :by: (x âˆª (y âˆª z) -def) w
    âŒª _==_ âŒª w âˆˆ x âˆ¨ (w âˆˆ y âˆ¨ w âˆˆ z)
      :by: ap (w âˆˆ x âˆ¨_) $ prop-ext $ (y âˆª z -def) w
    âŒª _==_ âŒª w âˆˆ x âˆ¨ w âˆˆ y âˆ¨ w âˆˆ z
      :by: assoc (w âˆˆ x) (w âˆˆ y) (w âˆˆ z)
    âŒª _==_ âŒª w âˆˆ x âˆª y âˆ¨ w âˆˆ z
      :by: ap (_âˆ¨ w âˆˆ z) $ prop-ext $ sym $ (x âˆª y -def) w
    âŒª _â†”_ âŒª  w âˆˆ x âˆª y âˆª z
      :by: sym $ ((x âˆª y) âˆª z -def) w
  qed

instance
  Prefixâˆ© : âˆ€ {x} â†’ UniversalPrefix (_âˆ© x) _âŠ†_
  Postfixâˆª : âˆ€ {x} â†’ UniversalPostfix (_âˆª x) _âŠ†_

UniversalPrefix.prefix (Prefixâˆ© {z}) x y yâˆˆxâˆ©z =
  âˆ§left $ âŸ¶ ((x âˆ© z -def) y) yâˆˆxâˆ©z

UniversalPostfix.postfix (Postfixâˆª {z}) x y yâˆˆx =
  âŸµ ((x âˆª z -def) y) $ âˆ¨left yâˆˆx

infixl 148 _Î”_
_Î”_ : (x y : set) â†’ set
x Î” y = (x - y) âˆª (y - x)

open import Proposition.BinarySum

pair-âˆˆ-ğ’« : âˆ€ {X Y x y}
  (p : x âˆˆ X)
  (q : y âˆˆ Y)
  â†’ ---------------------
  [ x â¸´ y ] âˆˆ ğ’«[ X âˆª Y ]
pair-âˆˆ-ğ’« {X}{Y}{x}{y} p q =
  âŸµ (ğ’«[ X âˆª Y ]-def [ x â¸´ y ]) Î» z zâˆˆ[x,y] â†’
  âŸµ ((X âˆª Y -def) z) $
  âˆ¨-recursion (Î» { (Id.refl .x) â†’ âˆ¨left p})
              (Î» { (Id.refl .y) â†’ âˆ¨right q}) $
  âŸ¶ ([ x â¸´ y ]-def z) zâˆˆ[x,y]

ordered-pair-âˆˆ-ğ’«Â² : âˆ€ {X Y x y}
  (p : x âˆˆ X)
  (q : y âˆˆ Y)
  â†’ ---------------------
  âŸ¦ x â¸´ y âŸ§ âˆˆ ğ’«[ X âˆª ğ’«[ X âˆª Y ] ]
ordered-pair-âˆˆ-ğ’«Â² {X}{Y}{x}{y} p q =
  pair-âˆˆ-ğ’« p (pair-âˆˆ-ğ’« p q)

private
  _!Ã—_ : (X Y : set) â†’
    Cor.set[ u âˆ¶ (âˆƒ Î» x â†’ âˆƒ Î» y â†’ u == âŸ¦ x â¸´ y âŸ§ âˆ§ x âˆˆ X âˆ§ y âˆˆ Y) ]

X !Ã— Y = !separate
  (Î» u â†’ F.âˆƒ Î» x â†’ F.âˆƒ Î» y â†’ u F.== âŸ¦ x â¸´ y âŸ§ F.âˆ§ x F.âˆˆ X F.âˆ§ y F.âˆˆ Y)
  (ğ’«[ X âˆª ğ’«[ X âˆª Y ] ] ,
   Î» {.(âŸ¦ x' â¸´ y' âŸ§) (x' , (y' , (Id.refl _ , x'âˆˆX , y'âˆˆY))) â†’
        ordered-pair-âˆˆ-ğ’«Â² x'âˆˆX y'âˆˆY})

infixl 160 _Ã—_
_Ã—_ : (X Y : set) â†’ set
X Ã— Y = elem (!choice (X !Ã— Y))

_Ã—_-def : âˆ€ X Y â†’
  âˆ€ z â†’
  z âˆˆ X Ã— Y â†” âˆƒ Î» x â†’ âˆƒ Î» y â†’ z == âŸ¦ x â¸´ y âŸ§ âˆ§ x âˆˆ X âˆ§ y âˆˆ Y
âŸ¶ ((X Ã— Y -def) z) p = âŸ¶ (âˆ§left (prop (!choice (X !Ã— Y))) z) p
âŸµ ((X Ã— Y -def) z) (x , (y , (Id.refl _ , xâˆˆX , yâˆˆY))) =
  âŸµ (âˆ§left (prop (!choice (X !Ã— Y))) âŸ¦ x â¸´ y âŸ§)
    (x , (y , (refl _ , xâˆˆX , yâˆˆY)))

open import Data.Nat

infixl 170 _-^-_
_-^-_ : (X : set)(m : â„•) â†’ set
X -^- 0 = ğ’«[ âˆ… ]
X -^- 1 = X
X -^- (m +2) = X -^- (m +1) Ã— X

is-_[_]-tuple : (m : â„•)(p : 1 < m)(x : set) â†’ Formula
is- 1 [ p ]-tuple x with s<sâ†’-<- p
... | ()
is- 2 [ p ]-tuple x = F.âˆƒ Î» u â†’ F.âˆƒ Î» v â†’ x F.== âŸ¦ u â¸´ v âŸ§
is- m +3 [ _ ]-tuple x = F.âˆƒ Î» u â†’ F.âˆƒ Î» v â†’
  x F.== âŸ¦ u â¸´ v âŸ§ F.âˆ§ is- m +2 [ ap suc z<s ]-tuple u

Rel : (m : â„•)(p : 1 < m) â†’ ğ’°â‚€ Ë™
Rel m p = Î£â‚š Î» R â†’ elem (F.â‹€ x âˆˆ R , is- m [ p ]-tuple x )

RelOn : (m : â„•)(X : set) â†’ ğ’°â‚€ Ë™
RelOn m X = Î£â‚š Î» R â†’ R âŠ† X -^- m

BinRel : ğ’°â‚€ Ë™
BinRel = Rel 2 (ap suc z<s)

is-bin-rel : (R : set) â†’ Formula
is-bin-rel R = F.â‹€ x âˆˆ R , is- 2 [ ap suc z<s ]-tuple x

variable
  R : BinRel

private
  dom' : (R : set) â†’ Cor.set[ u âˆ¶ (âˆƒ Î» v â†’ âŸ¦ u â¸´ v âŸ§ âˆˆ R) ]
  ran' : (R : set) â†’ Cor.set[ v âˆ¶ (âˆƒ Î» u â†’ âŸ¦ u â¸´ v âŸ§ âˆˆ R) ]

dom' R =
  !separate (Î» u â†’ F.âˆƒ Î» v â†’ âŸ¦ u â¸´ v âŸ§ F.âˆˆ R) (â‹ƒ R , p)
  where p : âˆ€ u (p : âˆƒ Î» v â†’ âŸ¦ u â¸´ v âŸ§ âˆˆ R) â†’ u âˆˆ â‹ƒ R
        p u (v , [u,v]âˆˆR) =
          âŸµ (â‹ƒ-def R u)
             (âŸ¦ u â¸´ v âŸ§ ,
               ([u,v]âˆˆR ,
                âŸµ ([ u â¸´ [ u â¸´ v ] ]-def u) $ âˆ¨left $ refl u))

dom : (R : set) â†’ set
dom R = elem (!choice (dom' R))

dom-def : âˆ€ R z â†’ z âˆˆ dom R â†” âˆƒ Î» v â†’ âŸ¦ z â¸´ v âŸ§ âˆˆ R
dom-def R = âˆ§left (prop (!choice (dom' R)))

ran' R =
  !separate (Î» v â†’ F.âˆƒ Î» u â†’ âŸ¦ u â¸´ v âŸ§ F.âˆˆ R ) (â‹ƒ â‹ƒ R , p)
  where p : âˆ€ v (p : âˆƒ Î» u â†’ âŸ¦ u â¸´ v âŸ§ âˆˆ R) â†’ v âˆˆ â‹ƒ â‹ƒ R
        p v (u , [u,v]âˆˆR) =
          âŸµ (â‹ƒ-def (â‹ƒ R) v) ([ u â¸´ v ] ,
            (âŸµ (â‹ƒ-def R [ u â¸´ v ]) (âŸ¦ u â¸´ v âŸ§ ,
              ([u,v]âˆˆR ,
               âŸµ ([ u â¸´ [ u â¸´ v ] ]-def [ u â¸´ v ]) $ âˆ¨right $ refl [ u â¸´ v ])) ,
             âŸµ ([ u â¸´ v ]-def v) $ âˆ¨right $ refl v))

ran : (R : set) â†’ set
ran R = elem (!choice (ran' R))

ran-def : âˆ€ R z â†’ z âˆˆ ran R â†” âˆƒ Î» u â†’ âŸ¦ u â¸´ z âŸ§ âˆˆ R
ran-def R = âˆ§left (prop (!choice (ran' R)))

field-of : (R : set) â†’ set
field-of R = dom R âˆª ran R

is-function : (f : set) â†’ Formula
is-function f = is-bin-rel f F.âˆ§ F.A Î» x â†’ F.A Î» y â†’ F.A Î» z â†’
  âŸ¦ x â¸´ y âŸ§ F.âˆˆ f F.âŸ¶
  âŸ¦ x â¸´ z âŸ§ F.âˆˆ f F.âŸ¶
  y F.== z

Function = Î£â‚š Î» f â†’ elem (is-function f)

open import Proposition.Decidable

module Classical where
  open import Axiom.ExcludedMiddle

  formula-decidable : (Ï• : Formula) â†’ Decidable (F.holds Ï•)
  formula-decidable Ï• = excluded-middle (F.holds Ï•)
open Classical

value-of_at_ : (f x : set) â†’ set
value-of f at x = elem (!choice p')
  where p' : âˆƒ! Î» v â†’
          F.holds ((is-function f F.âˆ§ x F.âˆˆ dom f F.âŸ¶ âŸ¦ x â¸´ v âŸ§ F.âˆˆ f) F.âˆ§
                   (F.Â¬ (is-function f F.âˆ§ x F.âˆˆ dom f) F.âŸ¶ v F.== âˆ…))
        p' with formula-decidable (is-function f F.âˆ§ x F.âˆˆ dom f)
        p' | true (rel , uniq , xâˆˆdom-f) with âŸ¶ (dom-def f x) xâˆˆdom-f
        p' | true q@(rel , uniq , xâˆˆdom-f) | v , p = v , (
          (Î» _ â†’ p) ,
          (Î» Â¬q â†’ âŠ¥-recursion _ $ Â¬q q) ,
          Î» { y (qâ†’[x,y]âˆˆf , _) â†’ uniq x y v (qâ†’[x,y]âˆˆf q) p})
        p' | false Â¬p = âˆ… , (
          (Î» p â†’ âŠ¥-recursion _ $ Â¬p p) ,
          (Î» _ â†’ refl âˆ…) ,
          Î» _ p â†’ âˆ§right p Â¬p)

_âˆ¶_âŸ¶_ : (f X Y : set) â†’ Formula
f âˆ¶ X âŸ¶ Y = is-function f F.âˆ§ dom f F.== X F.âˆ§ ran f F.âŠ† Y

infixl 170 _^_
_^_ : (Y X : set) â†’ set
Y ^ X = separate (Î» f â†’ f âˆ¶ X âŸ¶ Y) (ğ’«[ X Ã— Y ] , p)
  where p : âˆ€ f (q : elem (f âˆ¶ X âŸ¶ Y)) â†’ f âˆˆ ğ’«[ X Ã— Y ]
        p f (f-rel , _ , dom-f==X , ran-fâŠ†Y) =
          âŸµ (ğ’«[ X Ã— Y ]-def f) $
          Î» [x,y] [x,y]âˆˆf â†’ âŸµ ((X Ã— Y -def) [x,y]) $
          q [x,y] [x,y]âˆˆf
          where q : âˆ€ [x,y] ([x,y]âˆˆf : [x,y] âˆˆ f) â†’
                  âˆƒ Î» x â†’ âˆƒ Î» y â†’
                  [x,y] == âŸ¦ x â¸´ y âŸ§ âˆ§ x âˆˆ X âˆ§ y âˆˆ Y
                q [x,y] [x,y]âˆˆf with f-rel [x,y] [x,y]âˆˆf
                q .(âŸ¦ x â¸´ y âŸ§) [x,y]âˆˆf | x , (y , Id.refl _) = x , (y , (
                  refl âŸ¦ x â¸´ y âŸ§ ,
                  Id.coe (ap (x âˆˆ_) dom-f==X) $ âŸµ (dom-def f x) (y , [x,y]âˆˆf) ,
                  (proof y
                    âŒª _âˆˆ_ âŒª ran f :by: âŸµ (ran-def f y) (x , [x,y]âˆˆf)
                    âŒª _âŠ†_ âŒª Y     :by: ran-fâŠ†Y
                   qed)
                  ))
